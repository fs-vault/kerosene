plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
}

group 'com.firestartermc'
version '5.1.4'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://ci.ender.zone/plugin/repository/everything/' }
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://jitpack.io/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.17-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot:1.17-R0.1-SNAPSHOT'
    compileOnly 'net.ess3:EssentialsX:2.17.2'
    compileOnly 'com.github.milkbowl:vaultapi:1.7'
    compileOnly 'com.mojang:authlib:1.5.21'
    implementation 'org.jetbrains:annotations:20.0.0'
    implementation 'it.unimi.dsi:fastutil:8.4.2'
    implementation "io.papermc:paperlib:1.0.5"
    implementation "com.zaxxer:HikariCP:3.4.5"
    implementation "io.lettuce:lettuce-core:5.3.3.RELEASE"
    implementation "co.aikar:acf-paper:0.5.0-SNAPSHOT"
    implementation 'me.lucko:commodore:1.10'
}

compileJava {
    sourceCompatibility = '16'
    targetCompatibility = '16'
}

shadowJar {
    relocate 'io.papermc.lib', 'com.firestartermc.kerosene.lib.paperlib'
    relocate 'com.zaxxer.hikari', 'com.firestartermc.kerosene.lib.hikari'
    relocate 'io.lettuce.core', 'com.firestartermc.kerosene.lib.lettuce'
    relocate 'it.unimi.dsi.fastutil', 'com.firestartermc.kerosene.lib.fastutil'
    relocate 'me.lucko.commodore', 'com.firestartermc.kerosene.lib.commodore'
    exclude 'org/checkerframework/**'
    exclude 'org/jetbrains/annotations/**'
    exclude 'org/intellij/lang/**'
    exclude 'org/slf4j/**'
    exclude 'io/netty/**'

    dependencies {
        exclude(dependency('com.mojang:brigadier'))
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

assemble.dependsOn shadowJar